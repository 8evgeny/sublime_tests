Пример 16.1. qnumcat
#!/usr/bin/perl
# qnumcat - установка сцепленных выходных фильтров

number();            # Установить для STDOUT фильтр number
quote();             # Установить для STDOUT фильтр quote

while (<>) {         # Имитировать /bin/cat
    print;
} 
close STDOUT;        # Вежливо сообщить потомкам о завершении
exit;

sub number {
    my $pid;
    return if $pid = open STDOUT, "|-";
    die "cannot fork: $!" unless defined $pid;
    while (<STDIN>) { printf "%d: %s", $., $_ } 
    exit;
} 

sub quote {
    my $pid;
    return if $pid = open STDOUT, "|-";
    die "cannot fork: $!" unless defined $pid;
    while (<STDIN>) { print "> $_" } 
    exit;
} 

