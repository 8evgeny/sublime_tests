#!/usr/bin/perl -w
# fixstyle - замена строк секции <DATA> парными строками
# использование: $0 [-v] [файлы...]
use strict;
my $verbose = (@ARGV && $ARGV[0] eq '-v' && shift);
if (@ARGV) {
    $^I = ".orig";         # сохранить старые файлы
} else {
    warn "$0: Reading from stdin\n" if -t STDIN;
}
my $code = "while (<>) {\n";
# „итать данные и строить код дл€ eval
while (<DATA>) {
    chomp;
    my ($in, $out) = split /\s*=>\s*/;
    next unless $in && $out;
    $code .= "s{\\Q$in\\E}{$out}g";
    $code .= "&& printf STDERR qq($in => $out at \$ARGV line \$.\\n)"
                                                        if $verbose;
    $code .= ";\n";
}
$code .= "print;\n}\n";
eval "{ $code } 1" || die;
__END__
analysed       => analyzed
built-in       => builtin
chastized      => chastised
commandline    => command-line
de-allocate    => deallocate
dropin         => drop-in
hardcode       => hard-code
meta-data      => metadata
multicharacter => multi-character
multiway       => multi-way
non-empty      => nonempty
non-profit     => nonprofit
non-trappable  => nontrappable
pre-define     => predefine
preextend      => pre-extend
re-compiling   => recompiling
reenter        => re-enter
turnkey        => turn-key
