Пример 7.10. File::LockDir
package File::LockDir;
# Модуль, обеспечивающий простейшую блокировку
# на уровне имен файлов без применения хитрых системных функций.
# Теоретически информация о каталогах синхронизируется через NFS.
# Испытания на прочность не проводились.
use strict;
use Exporter;
our (@ISA, @EXPORT);
@ISA      = qw(Exporter);
@EXPORT   = qw(nflock nunflock);
our ($Debug, $Check);
$Debug  ||= 0;  # Может определяться заранее
$Check  ||= 5;  # Может определяться заранее
use Cwd;
use Fcntl;
use Sys::Hostname;
use File::Basename;
use File::stat;
use Carp;
my %Locked_Files = ();
# Применение: nflock(ФАЙЛ; ТАЙМАУТ)
sub nflock($;$) {
    my $pathname = shift;
    my $naptime  = shift || 0;
    my $lockname = name2lock($pathname);
    my $whosegot = "$lockname/owner";
    my $start    = time();
    my $missed   = 0;
    my $owner;
    # Если блокировка уже установлена, вернуться
    if ($Locked_Files{$pathname}) {
        carp "$pathname already locked";
        return 1
    }
    if (!-w dirname($pathname)) {
        croak "can't write to directory of $pathname";
    }
    while (1) {
        last if mkdir($lockname, 0777);

  confess "can't get $lockname: $!" if $missed++ > 10
                        && !-d $lockname;
        if ($Debug) {{
            open($owner, "< $whosegot") || last; # exit "if"!
            my $lockee = <$owner>;
            chomp($lockee);
            printf STDERR "%s $0\[$$]: lock on %s held by %s\n",
                scalar(localtime), $pathname, $lockee;
            close $owner;
        }}
        sleep $Check;
        return if $naptime && time > $start+$naptime;
    }
    sysopen($owner, $whosegot, O_WRONLY|O_CREAT|O_EXCL)
                            or croak "can't create $whosegot: $!";
    printf $owner "$0\[$$] on %s since %s\n",
            hostname(), scalar(localtime);
    close($owner)                
		or croak "close $whosegot: $!";
    $Locked_Files{$pathname}++;
    return 1;
}
# Освободить заблокированный файл
sub nunflock($) {
    my $pathname = shift;
    my $lockname = name2lock($pathname);
    my $whosegot = "$lockname/owner";
    unlink($whosegot);
    carp "releasing lock on $lockname" if $Debug;
    delete $Locked_Files{$pathname};
    return rmdir($lockname);
}
# Вспомогательная функция
sub name2lock($) {
    my $pathname = shift;
    my $dir  = dirname($pathname);
    my $file = basename($pathname);
    $dir = getcwd() if $dir eq ".";
    my $lockname = "$dir/$file.LOCKDIR";
    return $lockname;
}
# Ничего не забыли?
END {
    for my $pathname (keys %Locked_Files) {
        my $lockname = name2lock($pathname);
        my $whosegot = "$lockname/owner";
        carp "releasing forgotten $lockname";
        unlink($whosegot);
        return rmdir($lockname);
    }
}
1;

