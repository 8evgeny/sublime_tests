Пример 6.1. resname
#!/usr/bin/perl -p
# resname – заменить все имена в стиле "foo.bar.com" во входном потоке
# на "foo.bar.com [204.148.40.9]" (или аналогичными)

use Socket;                 # Загрузить inet_addr
s{                          #
    (                       # Сохранить имя хоста в $1
        (?:                 # Скобки только для группировки
            (?! [-_]  )     # Ни подчеркивание, ни дефис
            [\w-] +         # Компонент имени хоста
            \.              # и точка домена
        ) +                 # Повторяется несколько раз
        [A-Za-z]            # Следующий символ должен быть буквой
        [\w-] +             # Завершающая часть домена
    )                       # Конец записи $1
}{                          # Заменить следующим:
    "$1 " .                 # Исходная часть плюс пробел
           ( ($addr = gethostbyname($1))   # Если имеется адрес
            ? "[" . inet_ntoa($addr) . "]" #        отформатировать
            : "[???]"                      # иначе пометить как сомнительный
           )
}gex;               # /g – глобальная замена
                    # /e - выполнение
                    # /x – улучшенное форматирование

