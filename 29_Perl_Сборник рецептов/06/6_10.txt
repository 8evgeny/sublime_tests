Пример 6.10. urlify
#!/usr/bin/perl
# urlify – оформление URL-подобных конструкций в виде ссылок HTML
$protos = '(http|telnet|gopher|file|wais|ftp)';
$ltrs = '\w';
$gunk = '/#~:.?+=&%@!\-';
$punc = '.:?\-';
$any  = "${ltrs}${gunk}${punc}";
while (<>) {
    s{
      \b                    # Начать с границы слова
      (                     # Начать сохранение $1  {
       $protos   :          # Искать имя ресурса и двоеточие,
       [$any] +?            # за которыми следует один или более
                            # любых допустимых символов, но
                            # проявлять умеренность и брать лишь то,
                            # что действительно необходимо ....
      )                     # Завершить сохранение $1  }
      (?=                   # Опережающая проверка без смещения
       [$punc]*             # либо 0, либо знак препинания
       [^$any]              #   далее следует символ, не входящий в url
       |                    # или 
       $                    #   конец строки
      )
     }{<A HREF="$1">$1</A>}igox;
    print;
}

