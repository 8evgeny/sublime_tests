Пример 15.5. rep
#!/usr/bin/perl -w
# rep - циклическое выполнение команды
use strict;
use Curses;

my $timeout = 10;
if (@ARGV && $ARGV[0] =~ /^-(\d+\.?\d*)$/) { 
    $timeout = $1; 
    shift; 
} 

die "usage: $0 [ -timeout ] cmd args\n" unless @ARGV;
    
initscr();          # Инициализировать экран
noecho();           
cbreak(); 
nodelay(1);         # Чтобы функция getch() выполнялась без блокировки

$SIG{INT} = sub { done("Ouch!") };
sub done { endwin(); print "@_\n"; exit; }

while (1) {
    while ((my $key = getch()) ne ERR) {    # Возможен ввод 
        done("See ya") if $key eq 'q'       # нескольких символов
    }
    my @data = `(@ARGV) 2>&1`;              # Вывод+ошибки
    for (my $i = 0; $i < $LINES; $i++) {
        addstr($i, 0, $data[$i] || ' ' x $COLS);
    } 

    standout();
    addstr($LINES-1, $COLS - 24, scalar localtime);
    standend();

    move(0,0); 
    refresh();                              # Обновить экран
    my ($in, $out) = ('', '');
    vec($in,fileno(STDIN),1) = 1;           # Искать символ в stdin
    select($out = $in,undef,undef,$timeout);# Ожидание
}

